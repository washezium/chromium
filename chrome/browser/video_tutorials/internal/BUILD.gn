# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

source_set("internal") {
  sources = [
    "proto_conversions.cc",
    "proto_conversions.h",
    "tutorial_service_impl.cc",
    "tutorial_service_impl.h",
  ]

  public_deps = [ "//components/image_fetcher/core" ]

  deps = [
    "//base",
    "//chrome/browser/video_tutorials:public",
    "//chrome/browser/video_tutorials/proto",
    "//components/leveldb_proto",
  ]

  if (is_android) {
    sources += [
      "android/tutorial_conversion_bridge.cc",
      "android/tutorial_conversion_bridge.h",
      "android/video_tutorial_service_bridge.cc",
      "android/video_tutorial_service_bridge.h",
    ]

    deps += [ ":jni_headers" ]
  }
}

if (is_android) {
  android_library("java") {
    visibility = [
      ":*",
      "//chrome/android:chrome_all_java",
    ]

    sources = [
      "android/java/src/org/chromium/chrome/browser/video_tutorials/bridges/TutorialConversionBridge.java",
      "android/java/src/org/chromium/chrome/browser/video_tutorials/bridges/VideoTutorialServiceBridge.java",
      "android/java/src/org/chromium/chrome/browser/video_tutorials/bridges/VideoTutorialServiceFactory.java",
    ]

    deps = [
      "//base:base_java",
      "//base:jni_java",
      "//chrome/browser/profiles/android:java",
      "//chrome/browser/video_tutorials:java",
      "//third_party/android_deps:androidx_annotation_annotation_java",
    ]

    annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  }

  generate_jni("jni_headers") {
    visibility = [ ":*" ]

    sources = [
      "android/java/src/org/chromium/chrome/browser/video_tutorials/bridges/TutorialConversionBridge.java",
      "android/java/src/org/chromium/chrome/browser/video_tutorials/bridges/VideoTutorialServiceBridge.java",
      "android/java/src/org/chromium/chrome/browser/video_tutorials/bridges/VideoTutorialServiceFactory.java",
    ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [ "proto_conversions_unittest.cc" ]

  deps = [
    ":internal",
    "//base",
    "//base/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
