# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
  import("//chrome/android/features/android_library_factory_tmpl.gni")
}

group("video_tutorials") {
  public_deps = [
    ":factory",
    ":public",
  ]

  deps = [ "internal" ]
}

source_set("public") {
  sources = [
    "switches.cc",
    "switches.h",
    "tutorial.cc",
    "tutorial.h",
    "video_tutorial_service.h",
  ]

  deps = []

  public_deps = [
    "//base",
    "//components/keyed_service/core",
    "//url:url",
  ]
}

source_set("factory") {
  sources = [
    "tutorial_factory_helper.cc",
    "tutorial_factory_helper.h",
  ]

  deps = [
    ":public",
    "internal:internal",
    "//chrome/browser/video_tutorials/proto",
    "//components/leveldb_proto",
  ]
}

if (is_android) {
  android_library("java") {
    sources = [
      "android/java/src/org/chromium/chrome/browser/video_tutorials/Tutorial.java",
      "android/java/src/org/chromium/chrome/browser/video_tutorials/VideoTutorialService.java",
    ]

    deps = [
      "//base:base_java",
      "//components/browser_ui/widget/android:java",
      "//third_party/android_deps:androidx_annotation_annotation_java",
    ]

    srcjar_deps = [ ":jni_enums" ]
  }

  java_cpp_enum("jni_enums") {
    visibility = [ "*" ]

    sources = [ "tutorial.h" ]
  }

  android_library_factory("factory_java") {
    # These deps will be inherited by the resulting android_library target.
    deps = [
      ":java",
      "//chrome/browser/profiles/android:java",
    ]

    # This internal file will be replaced by a generated file so the resulting
    # android_library target does not actually depend on this internal file.
    sources = [ "internal/android/java/src/org/chromium/chrome/browser/video_tutorials/bridges/VideoTutorialServiceFactory.java" ]
  }
}

group("unit_tests") {
  testonly = true

  deps = [
    ":video_tutorials_unit_tests",
    "internal:unit_tests",
  ]
}

source_set("video_tutorials_unit_tests") {
  testonly = true
  sources = [ "tutorial_unittest.cc" ]
  deps = [
    ":public",
    "//base/test:test_support",
    "//testing/gtest",
  ]
}
