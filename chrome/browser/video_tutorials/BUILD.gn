# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

if (is_android) {
  java_group("java") {
    deps = [ ":public_java" ]
  }
}

group("video_tutorials") {
  public_deps = [
    ":factory",
    ":public",
  ]

  deps = [ "internal" ]
}

source_set("public") {
  sources = [
    "switches.cc",
    "switches.h",
    "tutorial.cc",
    "tutorial.h",
    "video_tutorial_service.h",
  ]

  deps = []

  public_deps = [
    "//base",
    "//components/keyed_service/core",
    "//url:url",
  ]

  if (is_android) {
    sources += [
      "android/tutorial_conversion_bridge.cc",
      "android/tutorial_conversion_bridge.h",
      "android/video_tutorial_service_bridge.cc",
      "android/video_tutorial_service_bridge.h",
    ]

    deps += [ ":jni_headers" ]
  }
}

source_set("factory") {
  sources = [
    "tutorial_factory_helper.cc",
    "tutorial_factory_helper.h",
  ]

  deps = [
    ":public",
    "internal:internal",
    "//chrome/browser/video_tutorials/proto",
    "//components/leveldb_proto",
  ]
}

if (is_android) {
  android_library("public_java") {
    sources = [
      "android/java/src/org/chromium/chrome/browser/video_tutorials/Tutorial.java",
      "android/java/src/org/chromium/chrome/browser/video_tutorials/VideoTutorialService.java",
      "android/java/src/org/chromium/chrome/browser/video_tutorials/bridges/TutorialConversionBridge.java",
      "android/java/src/org/chromium/chrome/browser/video_tutorials/bridges/VideoTutorialServiceBridge.java",
    ]

    deps = [
      "//base:base_java",
      "//base:jni_java",
      "//components/browser_ui/widget/android:java",
      "//third_party/android_deps:androidx_annotation_annotation_java",
    ]

    srcjar_deps = [ ":jni_enums" ]

    annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  }

  generate_jni("jni_headers") {
    visibility = [ ":*" ]

    sources = [
      "android/java/src/org/chromium/chrome/browser/video_tutorials/bridges/TutorialConversionBridge.java",
      "android/java/src/org/chromium/chrome/browser/video_tutorials/bridges/VideoTutorialServiceBridge.java",
    ]
  }

  java_cpp_enum("jni_enums") {
    visibility = [ "*" ]

    sources = [ "tutorial.h" ]
  }
}

group("unit_tests") {
  testonly = true

  deps = [
    ":video_tutorials_unit_tests",
    "internal:unit_tests",
  ]
}

source_set("video_tutorials_unit_tests") {
  testonly = true
  sources = [ "tutorial_unittest.cc" ]
  deps = [
    ":public",
    "//base/test:test_support",
    "//testing/gtest",
  ]
}
