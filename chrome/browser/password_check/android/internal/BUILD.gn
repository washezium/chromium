# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//chrome/android/features/android_library_factory_tmpl.gni")

_factory_sources = [ "java/src/org/chromium/chrome/browser/password_check/PasswordCheckFactory.java" ]
_public_target = "//chrome/browser/password_check/android:public_java"

android_library_factory("public_factory_java") {
  # These deps will be inherited by the resulting android_library target.
  deps = [ _public_target ]

  # This internal file will be replaced by a generated file so the resulting
  # android_library target does not actually depend on this internal file.
  sources = _factory_sources
}

android_library("internal_factory_java") {
  deps = [ ":internal_java" ]
  sources = _factory_sources
}

_ui_factory_sources = [ "java/src/org/chromium/chrome/browser/password_check/PasswordCheckComponentUiFactory.java" ]
_public_ui_target = "//chrome/browser/password_check/android:public_ui_java"

android_library_factory("public_ui_factory_java") {
  deps = [
    "//third_party/android_deps:androidx_fragment_fragment_java",
    "//third_party/android_deps:androidx_preference_preference_java",
    _public_ui_target,
  ]
  sources = _ui_factory_sources
}

android_library("internal_ui_factory_java") {
  deps = [
    ":internal_java",
    _public_target,
  ]
  sources = _ui_factory_sources
}

android_library("internal_java") {
  deps = [
    "//chrome/android:chrome_app_java_resources",
    "//chrome/android:chrome_java",
    "//chrome/browser/password_check/android:public_ui_java",
    "//chrome/browser/profiles/android:java",
    "//chrome/browser/settings:java",
    "//chrome/browser/ui/android/strings:ui_strings_grd",
    "//third_party/android_deps:androidx_fragment_fragment_java",
    "//third_party/android_deps:androidx_preference_preference_java",
    _public_target,
  ]
  sources = [
    "java/src/org/chromium/chrome/browser/password_check/PasswordCheckCoordinator.java",
    "java/src/org/chromium/chrome/browser/password_check/PasswordCheckImpl.java",
  ]
}
